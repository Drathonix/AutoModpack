import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.quiltmc.org/repository/release" }
        maven { url "https://files.minecraftforge.net/maven/" }
        maven { url "https://jitpack.io" }
        mavenLocal()
    }
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": {
                    useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
                    break
                }
            }
        }
    }
}

include(":common")
include(":server-host")

def preload_modules = [
        "common",
        "fabric",
        "forge",
        "quilt",
]

for (String module : preload_modules) {
    include(":core-$module")

    def project = project(":core-$module")
    project.projectDir = file("core/$module")
    if (module == "common") {
        project.buildFileName = "../core-common.gradle"
    } else {
        project.buildFileName = "../loader-common.gradle"
    }
}

def settings = new JsonSlurper().parseText(file('settings.json').text)
for (String version : settings.versions) {
    include(":$version")

    def project = project(":$version")
    project.projectDir = file("versions/$version")
    project.buildFileName = "../build.gradle"
}